## Copyright 2019-2021 Intel Corporation
## SPDX-License-Identifier: Apache-2.0

set(COMPONENT_NAME rkcommon)

set(COMPONENT_PATH ${INSTALL_DIR_ABSOLUTE})
if (INSTALL_IN_SEPARATE_DIRECTORIES)
  set(COMPONENT_PATH ${INSTALL_DIR_ABSOLUTE}/${COMPONENT_NAME})
endif()

set(RKCOMMON_HASH_ARGS "")
if (NOT "${RKCOMMON_HASH}" STREQUAL "")
  set(RKCOMMON_HASH_ARGS URL_HASH SHA256=${RKCOMMON_HASH})
endif()

if (NOT "$ENV{RKCOMMON_GIT_SHA}" STREQUAL "")
  set(RKCOMMON_GIT_SHA GIT_TAG $ENV{RKCOMMON_GIT_SHA})
  set(RKCOMMON_GIT_REPOSITORY GIT_REPOSITORY $ENV{CI_SERVER_PROTOCOL}://gitlab-ci-token:$ENV{CI_JOB_TOKEN}@$ENV{CI_SERVER_HOST}/$ENV{RKCOMMON_PROJECT_PATH}.git)

  # Clear variables for classic download source code
  # in favour of git.
  set(RKCOMMON_HASH_ARGS "")
  set(RKCOMMON_URL "")
endif()

ExternalProject_Add(${COMPONENT_NAME}
  PREFIX ${COMPONENT_NAME}
  DOWNLOAD_DIR ${COMPONENT_NAME}
  STAMP_DIR ${COMPONENT_NAME}/stamp
  SOURCE_DIR ${COMPONENT_NAME}/src
  BINARY_DIR ${COMPONENT_NAME}/build

  ${RKCOMMON_GIT_REPOSITORY}
  ${RKCOMMON_GIT_SHA}

  URL "${RKCOMMON_URL}"
  ${RKCOMMON_HASH_ARGS}

  CMAKE_ARGS
    -DCMAKE_PREFIX_PATH:PATH=${CMAKE_PREFIX_PATH}
    -DINSTALL_DEPS=OFF
    -DBUILD_TESTING=OFF
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_INSTALL_PREFIX:PATH=${COMPONENT_PATH}
    -DCMAKE_INSTALL_INCLUDEDIR=${CMAKE_INSTALL_INCLUDEDIR}
    -DCMAKE_INSTALL_LIBDIR=${CMAKE_INSTALL_LIBDIR}
    -DCMAKE_INSTALL_DOCDIR=${CMAKE_INSTALL_DOCDIR}
    -DCMAKE_INSTALL_BINDIR=${CMAKE_INSTALL_BINDIR}
    -DCMAKE_BUILD_TYPE=Release
    $<$<BOOL:${BUILD_TBB}>:-DRKCOMMON_TBB_ROOT=${TBB_PATH}>
  BUILD_COMMAND ${DEFAULT_BUILD_COMMAND}
  BUILD_ALWAYS ${ALWAYS_REBUILD}
)

if (BUILD_TBB)
  ExternalProject_Add_StepDependencies(${COMPONENT_NAME} configure tbb)
endif()

add_to_prefix_path(${COMPONENT_PATH})

